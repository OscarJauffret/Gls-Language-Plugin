{
  parserClass="com.gls.glsplugin.language.parser.GillesParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Gilles"
  psiImplClassSuffix="Impl"
  psiPackage="com.gls.glsplugin.language.psi"
  psiImplPackage="com.gls.glsplugin.language.psi.impl"

  elementTypeHolderClass="com.gls.glsplugin.language.psi.GillesTypes"
  elementTypeClass="com.gls.glsplugin.language.psi.GillesElementType"
  tokenTypeClass="com.gls.glsplugin.language.psi.GillesTokenType"

  psiImplUtilClass="com.gls.glsplugin.language.psi.impl.GillesPsiImplUtil"

  tokens=[
    PROGNAME = "regexp:[A-Z]([a-z]|_)*"
    VARNAME = "regexp:[a-z]([a-z]|\\d)*"
    NUMBER = "regexp:\d+"
    LET = "LET"
    BE = "BE"
    END = "END"
    COLON = ":"
    ASSIGN_OP = "="
    LPAREN = "("
    RPAREN = ")"
    MINUS = "-"
    PLUS = "+"
    TIMES = "*"
    DIVIDE = "/"
    IF_INSTR = "IF"
    THEN = "THEN"
    ELSE = "ELSE"
    LBRACK = "{"
    RBRACK = "}"
    IMPLIES = "->"
    PIPE = "|"
    EQUAL = "=="
    SMALEQ = "<="
    SMALLER = "<"
    WHILE_INSTR = "WHILE"
    REPEAT = "REPEAT"
    OUTPUT_OP = "OUT"
    INPUT_OP = "IN"
]
}

GillesFile ::= Program?;

Program ::= "LET" PROGNAME "BE" Code "END"
Code ::= (Instruction ":")*;
Instruction ::= Assign | If | While | Output | Input;

Assign ::= VARNAME ASSIGN_OP ExprArith {
    pin=3
    recoverWhile="recover_property"
    mixin="com.gls.glsplugin.language.psi.impl.GillesNamedElementImpl"
    implements="com.gls.glsplugin.language.psi.GillesNamedElement"
    methods=[getKey getValue getName setName getNameIdentifier]
}

ExprArith ::= Prod ExprArithTail;
ExprArithTail ::= ("+" Prod ExprArithTail | "-" Prod ExprArithTail)? ;
Prod ::= Factor ProdTail;
ProdTail ::= ("*" Factor ProdTail | "/" Factor ProdTail)? ;
Factor ::= "-"Factor | Atom;
Atom ::= VARNAME | NUMBER | "(" ExprArith ")";

If ::= IF_INSTR "{"Cond"}" "THEN" Code IfTail;
IfTail ::= "ELSE" Code "END" | "END";

Cond ::= FinalCond CondTail;
CondTail ::= (IMPLIES Cond)?;
FinalCond ::= PIPE Cond PIPE | ExprArith Comp ExprArith;

Comp ::= "==" | "<=" | "<";
While ::= WHILE_INSTR "{"Cond"}" "REPEAT" Code "END";
Output ::= OUTPUT_OP "(" VARNAME ")";
Input ::= INPUT_OP "(" VARNAME ")";

